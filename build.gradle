// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext {
        // sdk versions
        compile_sdk_version = 30
        build_tools_version = "30.0.3"
        min_sdk_version = 21
        target_sdk_version = 30
        version_code = 13
        version_name = "1.0.3"
    }
    repositories {
        // Google's Maven repository
        google()
        mavenCentral()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:$buildGradleVersion"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
        classpath "com.google.dagger:hilt-android-gradle-plugin:$hiltVersion"

        // Google Services plugin (required for firebase)
        classpath 'com.google.gms:google-services:4.3.8'

        // firebase
        classpath 'com.google.firebase:firebase-crashlytics-gradle:2.7.0'
        classpath 'com.google.firebase:perf-plugin:1.4.0'
        classpath 'com.google.firebase:firebase-appdistribution-gradle:2.1.2'
    }
}
ext {
    getParam = { key ->
        def value = System.getenv(key) ?: project.findProperty("env.$key")
        if (value) {
            println "$key = $value"
        }
        return value
    }

    DATABASE_NAME = getParam("DATABASE_SONGS")

    SIGNING_KEY_DEBUG_PATH = getParam("SIGNING_KEY_DEBUG_PATH")
    SIGNING_KEY_DEBUG_PASSWORD = getParam("SIGNING_KEY_DEBUG_PASSWORD")
    SIGNING_KEY_DEBUG_KEY = getParam("SIGNING_KEY_DEBUG_KEY")
    SIGNING_KEY_DEBUG_KEY_PASSWORD = getParam("SIGNING_KEY_DEBUG_KEY_PASSWORD")

    SIGNING_KEY_RELEASE_PATH = getParam("SIGNING_KEY_RELEASE_PATH")
    SIGNING_KEY_RELEASE_PASSWORD = getParam("SIGNING_KEY_RELEASE_PASSWORD")
    SIGNING_KEY_RELEASE_KEY = getParam("SIGNING_KEY_RELEASE_KEY")
    SIGNING_KEY_RELEASE_KEY_PASSWORD = getParam("SIGNING_KEY_RELEASE_KEY_PASSWORD")
}

subprojects {
    afterEvaluate {
        dependencies {
            // dependency injection (Dagger Hilt)
            annotationProcessor "com.google.dagger:hilt-android-compiler:$hiltVersion"
            annotationProcessor "androidx.hilt:hilt-compiler:$hiltAndroidXVersion"
            implementation "com.google.dagger:hilt-core:$hiltVersion"

            // Asynchronous programming (Reactive Extensions)
            implementation "io.reactivex.rxjava3:rxandroid:$rxAndroidVersion"
            implementation "com.github.akarnokd:rxjava3-bridge:$rxAndroidVersion"
            implementation "io.reactivex.rxjava3:rxjava:$rxJavaVersion"

            // database (Room)
            implementation "androidx.room:room-runtime:$roomVersion"
            annotationProcessor "androidx.room:room-compiler:$roomVersion"
            implementation "androidx.room:room-rxjava2:$roomVersion"
        }
    }
}

allprojects {
    repositories {
        // Google's Maven repository
        google()
        mavenCentral()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
